buildscript {
    ext.kotlin_version = '1.0.3'
    ext.dokka_version = '0.9.9'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7'
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'

def _group = 'com.importre'
def _artifact = 'kotlin-unwrap'
def _version = '0.1.0'
def _githubUrl = 'https://github.com/importre/kotlin-unwrap'

group _group
version _version

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

repositories {
    jcenter()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
    classifier = 'javadoc'
    from "$buildDir/dokka"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
}

publishing {
    publications {
        KotlinUnwrapPublish(MavenPublication) {
            from components.java
            groupId _group
            artifactId _artifact
            version _version
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty("user") ? project.user : ""
    key = project.hasProperty("key") ? project.key : ""
    publications = ['KotlinUnwrapPublish']
    publish = true
    // dryRun = true

    pkg {
        repo = 'maven'
        name = _artifact
        licenses = ['Apache-2.0']
        vcsUrl = "${_githubUrl}.git"
        publicDownloadNumbers = true

        version {
            name = _version
            vcsTag = _version
            gpg {
                sign = true
                passphrase = project.hasProperty("passphrase") ? project.passphrase : ""
            }
        }
    }
}
